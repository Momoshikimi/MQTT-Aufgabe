#include <WiFi.h>
#include <DHT.h>
#include <ArduinoJson.h>
#include <PubSubClient.h>

// WiFi-Konfigurationsdaten
const char* ssid = "Wokwi-GUEST";          // Dein WLAN-SSID
const char* password = "";  // Dein WLAN-Passwort

// MQTT-Konfigurationsdaten
const char* mqtt_server = "broker.hivemq.com"; // Adresse des MQTT-Servers
const int mqtt_port = 1883; // Port des MQTT-Servers (standardmäßig 1883)
const char* topic = "sensor/daten";  // Das MQTT-Thema, auf dem die Daten veröffentlicht werden

#define DHTPIN 4    // Anschluss des DHT-Sensors an Pin 4
#define DHTTYPE DHT22   // Typ des DHT-Sensors
const int sensorPin = 2; // Anschluss des Sensors an Pin 2 des ESP32

DHT dht(DHTPIN, DHTTYPE);
StaticJsonDocument<200> jsonData; // JSON-Dokument erstellen
WiFiClient espClient;
PubSubClient client(espClient);

void setup_wifi() {
  delay(10);
  Serial.println();
  Serial.print("Verbinden mit ");
  Serial.println(ssid);

  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi verbunden.");
  Serial.println("IP Adresse: ");
  Serial.println(WiFi.localIP());
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Verbinden mit MQTT-Server ...");
    if (client.connect("ESP32Client")) {
      Serial.println("verbunden");
    } else {
      Serial.print("Fehler, rc=");
      Serial.print(client.state());
      Serial.println(" in 5 Sekunden erneut versuchen");
      delay(5000);
    }
  }
}

void setup() {
  pinMode(sensorPin, INPUT);
  Serial.begin(9600);
  dht.begin();
  Serial.println("Setup abgeschlossen. Sensoren initialisiert.");
  setup_wifi();
  client.setServer(mqtt_server, mqtt_port);
}

int timeBewegung = 0;
int timeSendung = 0;
bool Bewegung = false;

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  delay(1000);  
  float humidity = dht.readHumidity();
  float temperature = dht.readTemperature();
  
  if (isnan(humidity) || isnan(temperature)) {
    Serial.println("Fehler beim Lesen des DHT-Sensors!");
    return;
  }
  
  Serial.print("Luftfeuchtigkeit: ");
  Serial.print(humidity);
  Serial.print("%, Temperatur: ");
  Serial.print(temperature);
  Serial.println("°C");

  if (digitalRead(sensorPin) == HIGH) {
    if (timeBewegung == 10) {
      timeBewegung = 0; 
      Bewegung = true;
      Serial.println("Bewegung erkannt.");
    } else {
      timeBewegung++; 
      Bewegung = true;
    }
  } else {
    Bewegung = false;
    timeBewegung = 0;
    Serial.println("Keine Bewegung.");
  }
  
  if (timeSendung == 60) {
    // Umwandlung in ein JSON-String
    String jsonString = json(humidity, temperature, Bewegung);
    Serial.println("Senden der Daten:");
    Serial.println(jsonString);

    // Veröffentlichen der Daten auf dem MQTT-Server
    if (client.publish(topic, jsonString.c_str())) {
      Serial.println("Daten erfolgreich gesendet.");
    } else {
      Serial.println("Fehler beim Senden der Daten.");
    }

    timeSendung = 0; // Zurücksetzen der Zeitvariable
  } else {
    timeSendung++;
  }
}

String json(float humidity, float temperature, bool Bewegung) {
  jsonData["schule"] = "HWS";
  jsonData["bau"] = "C";
  jsonData["stock"] = "1.OG";
  jsonData["raum"] = "107";
  jsonData["temperatur"] = temperature;
  jsonData["luftfeuchtigkeit"] = humidity;
  jsonData["anwesenheit"] = Bewegung; // Verfügbarkeit von Personen
  String jsonString;
  serializeJson(jsonData, jsonString);
  return jsonString;
}
